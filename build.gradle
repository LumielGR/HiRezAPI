plugins {
    id "idea"
    id "maven"
    id "jacoco"
    id "maven-publish"
    id "net.ltgt.apt" version "0.15"
    id "com.jfrog.bintray" version "1.8.0"
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
}

ext {
    versions = [
//            immutables: "2.5.6",
            // Common
            okhttp         : "3.10.0",
            jsoup          : "1.11.3",
            spring_platform: "Cairo-RELEASE"
    ]
}

allprojects {
    group = 'com.github.stachu540'
    version = '2.1.0-SNAPSHOT'

    repositories {
        jcenter()
    }

    plugins.withType(JavaPlugin) {
        plugins.withId("net.ltgt.apt-idea") {
            afterEvaluate {
                project.idea.module.apt.addAptDependencies = false
            }
        }
    }

    configurations { codacy }
}

subprojects {
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "net.ltgt.apt"
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.bintray"
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext.subprojectName = (project.name == "HiRezApi") ? "HiRezApi" : "HiRezApi-${project.name}"

    sourceSets {
        main {
            java {
                srcDirs = ['build/generated/source/apt/main', 'src/main/java']
            }
        }
    }

    dependencyManagement {
        imports {
            mavenBom "io.spring.platform:platform-bom:${versions.spring_platform}"
        }
    }

    dependencies {
        compile "org.slf4j:slf4j-api"
//        annotationProcessor "org.immutables:value:${versions.immutables}"
//        compileOnly "org.immutables:value:${versions.immutables}"
        annotationProcessor "org.projectlombok:lombok"
        compileOnly "org.projectlombok:lombok"

        testCompile "junit:junit"
        testCompile "org.mockito:mockito-core"
        testCompile "ch.qos.logback:logback-classic"

        codacy "com.codacy:codacy-coverage-reporter:4.0.0"
    }

    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        baseName = 'HiRezApi-' + baseName
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        baseName = 'HiRezApi-' + baseName
        from javadoc.destinationDir
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    publishing {
        publications {
            mainProject(MavenPublication) {
                from project.components.java
                groupId group
                artifactId subprojectName
                version version
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_API_KEY')
        publications = ['mainProject']
        pkg {
            repo = 'Java'
            name = 'HiRezAPI'
            userOrg = user
            desc = 'Java-Based API Wrapper for Hi-Rez Studios games.'
            websiteUrl = 'https://github.com/stachu540/HiRezAPI'
            vcsUrl = 'https://github.com/stachu540/HiRezAPI.git'
            issueTrackerUrl = 'https://github.com/stachu540/HiRezAPI/issues'
            licenses = ['MIT']
            labels = ['hi-rez', 'api-wrapper', 'smite', 'paladins']
            publicDownloadNumbers = true
            version {
                name = project.version
            }
        }
    }
}

task codacyCoverage(type: JavaExec) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "report",
            "-l",
            "Java",
            "-r",
            "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    ]
    dependsOn(task("jacocoTestReport"))
}

wrapper {
    gradleVersion = "4.7"
    distributionType = Wrapper.DistributionType.ALL
}