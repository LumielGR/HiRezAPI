plugins {
    id "idea"
    id "maven"
    id "jacoco"
    id "maven-publish"
    id "net.ltgt.apt" version "0.15"
    id "com.jfrog.bintray" version "1.8.0"
}

ext {
    versions = [
            lombok          : "1.16.20",
            slf4j           : "1.7.25",
            logback         : "1.2.3",
            jackson         : "2.9.5",
            rome            : "1.9.0",
            commons_lang3   : "3.7",
            okhttp          : "3.10.0",
            jsoup           : "1.11.3",
            codacy          : "4.0.0",
            mockito         : "2.18.3",
            junit           : "4.12"
    ]
}

allprojects {
    group = 'com.github.stachu540'
    version = '2.1.0'
    description = "Java-Based API Wrapper for Hi-Rez Studios games."

    repositories {
        jcenter()
    }

    plugins.withType(JavaPlugin) {
        plugins.withId("net.ltgt.apt-idea") {
            afterEvaluate {
                project.idea.module.apt.addAptDependencies = false
            }
        }
    }

}

subprojects {
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "net.ltgt.apt"
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.bintray"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        subprojectName = (project.name == "HiRezApi") ? "HiRezApi" : "HiRezApi-${project.name}"
        pomConf = {
            groupId = project.group
            artifactId = subprojectName
            version = project.version
            description = project.description
            name = "Hi-Rez API"
            url = "https://github.com/stachu540/HiRezAPI"
            licenses {
                license {
                    name "MIT Licence"
                    url "https://github.com/stachu540/HiRezAPI/blob/master/LICENSE"
                    distribution "repo"
                }
            }
            developers {
                developer {
                    id "stachu540"
                    name "Damian Staszewski"
                    email "damian@stachuofficial.pl"
                }
            }
            scm {
                url "https://github.com/stachu540/HiRezAPI.git"
            }
        }
    }

    sourceSets {
        main {
            java {
                srcDirs = ['build/generated/source/apt/main', 'src/main/java']
            }
        }
    }

    dependencies {
        compile "org.slf4j:slf4j-api:${versions.slf4j}"
        annotationProcessor "org.projectlombok:lombok:${versions.lombok}"
        compileOnly "org.projectlombok:lombok:${versions.lombok}"

        testCompile "junit:junit:${versions.junit}"
        testCompile "org.mockito:mockito-core:${versions.mockito}"
        testCompile "ch.qos.logback:logback-classic:${versions.logback}"
    }

    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        baseName = 'HiRezApi-' + baseName
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        baseName = 'HiRezApi-' + baseName
        from javadoc.destinationDir
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    publishing {
        publications {
            mainProject(MavenPublication) {
                from project.components.java
                groupId group
                artifactId subprojectName
                version version
                artifact sourcesJar
                artifact javadocJar
                description project.description
                pom.withXml {
                    def root = asNode()
                    root.children().last() + pomConf
                }
            }
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_API_KEY')
        publications = ['mainProject']
        pkg {
            repo = 'Java'
            name = 'HiRezAPI'
            userOrg = user
            desc = project.description
            websiteUrl = 'https://github.com/stachu540/HiRezAPI'
            vcsUrl = 'https://github.com/stachu540/HiRezAPI.git'
            issueTrackerUrl = 'https://github.com/stachu540/HiRezAPI/issues'
            licenses = ['MIT']
            labels = ['hi-rez', 'api-wrapper', 'smite', 'paladins']
            publicDownloadNumbers = true
            version {
                name = project.version
            }
        }
    }
}

wrapper {
    gradleVersion = "4.7"
    distributionType = Wrapper.DistributionType.ALL
}